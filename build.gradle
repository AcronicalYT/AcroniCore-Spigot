plugins {
    id 'java'
    id 'org.jetbrains.dokka' version '1.9.20'
}

group = 'uk.acronical'
version = '1.0.0-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
        maven {
            name = "spigotmc-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
    }
}

tasks.dokkaHtmlMultiModule.configure {
    moduleName.set("AcroniCore-Spigot")
    outputDirectory.set(layout.buildDirectory.dir("docs/api"))
}

tasks.register('prepareDocs') {
    dependsOn 'dokkaHtmlMultiModule'
    doLast {
        def docsDir = layout.buildDirectory.dir("docs/api").get().asFile
        if (!docsDir.exists()) docsDir.mkdirs()
        new File(docsDir, "CNAME").text = "core.acronical.uk"
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jetbrains.dokka'

    version = rootProject.version
    group = rootProject.group

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencies {
        compileOnly("org.spigotmc:spigot-api:1.21.1-R0.1-SNAPSHOT")

        compileOnly('org.jetbrains:annotations:24.1.0')

        testImplementation platform('org.junit:junit-bom:5.10.0')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    test {
        useJUnitPlatform()
    }

    tasks.matching { it.name.contains("dokkaHtmlPartial") }.configureEach {
        dokkaSourceSets {
            register("main") {
                def javaDir = new File(projectDir, "src/main/java")
                if (javaDir.exists()) {
                    sourceRoots.from(javaDir)
                    suppress.set(false)
                } else {
                    suppress.set(true)
                }
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                name = "AcronicalsRepo"
                def releasesRepoUrl = "https://maven.acronical.uk/releases"
                def snapshotsRepoUrl = "https://maven.acronical.uk/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username = project.findProperty("mavenUser") ?: System.getenv("MAVEN_USERNAME")
                    password = project.findProperty("mavenPassword") ?: System.getenv("MAVEN_PASSWORD")
                }
            }
        }
    }
}